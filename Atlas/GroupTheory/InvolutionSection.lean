import Mathlib.GroupTheory.OrderOfElement
import Mathlib.Algebra.Group.Subgroup.Finite
import Mathlib.GroupTheory.SpecificGroups.Cyclic

noncomputable section
namespace Atlas.GroupTheory

/-- An involution with nonidentity image splits a quotient of order two. -/
theorem involution_restriction_bijective {G Q : Type*} [Group G] [Group Q]
    [Finite G] [Finite Q] (f : G →* Q) (hq : Nat.card Q = 2)
    (s : G) (hs : s * s = 1) (hf : f s ≠ 1) :
    Function.Bijective (f.comp (Subgroup.zpowers s).subtype) := by
  classical
  have hs1 : s ≠ 1 := by intro h; exact hf (h ▸ f.map_one)
  have ho : orderOf s = 2 := orderOf_eq_prime (by simpa only [pow_two] using hs) hs1
  have hfo : orderOf (f s) = 2 := orderOf_eq_prime
    (by rw [pow_two,← map_mul,hs,map_one]) hf
  have ht : Subgroup.zpowers (f s) = ⊤ :=
    Subgroup.eq_top_of_card_eq _ (by rw [Nat.card_zpowers,hfo,hq])
  have hsur : Function.Surjective (f.comp (Subgroup.zpowers s).subtype) := by
    intro q
    have hmem : q ∈ Subgroup.zpowers (f s) := ht ▸ Subgroup.mem_top q
    obtain ⟨k,rfl⟩ := Subgroup.mem_zpowers_iff.mp hmem
    exact ⟨⟨s^k,(Subgroup.zpowers s).zpow_mem (Subgroup.mem_zpowers s) k⟩,f.map_zpow s k⟩
  exact hsur.bijective_of_nat_card_le (by rw [Nat.card_zpowers,ho,hq])

def involutionQuotientSection {G Q : Type*} [Group G] [Group Q]
    [Finite G] [Finite Q] (f : G →* Q) (hq : Nat.card Q = 2)
    (s : G) (hs : s * s = 1) (hf : f s ≠ 1) : Q →* G :=
  (Subgroup.zpowers s).subtype.comp
    (MulEquiv.ofBijective (f.comp (Subgroup.zpowers s).subtype)
      (involution_restriction_bijective f hq s hs hf)).symm.toMonoidHom

theorem involutionQuotientSection_rightInverse {G Q : Type*} [Group G] [Group Q]
    [Finite G] [Finite Q] (f : G →* Q) (hq : Nat.card Q = 2)
    (s : G) (hs : s * s = 1) (hf : f s ≠ 1) (q : Q) :
    f (involutionQuotientSection f hq s hs hf q) = q :=
  (MulEquiv.ofBijective (f.comp (Subgroup.zpowers s).subtype)
    (involution_restriction_bijective f hq s hs hf)).apply_symm_apply q

end Atlas.GroupTheory
